// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    And(a=address[14], b=address[13], out=isKeyboardSelected);
    Not(in=address[14], out=isRamSelected);
    Or(a=isKeyboardSelected, b=isRamSelected, out=isRamOrKeyboardSelected);
    Not(in=isRamOrKeyboardSelected, out=isScreenSelected);

    Or8Way(in=address[0..7], out=isFirst8NotZero);
    Or8Way(in=address[5..12], out=is5To12NotZero);
    Or(a=isFirst8NotZero, b=is5To12NotZero, out=is0To12NotZero);
    And(a=isKeyboardSelected, b=is0To12NotZero, out=isInvalidAddress);
    
    Keyboard(out=keyboardOutput);

    And(a=load, b=isScreenSelected, out=shouldWriteToScreen);
    Screen(in=in, load=shouldWriteToScreen, address=address[0..12], out=screenOutput);
    
    And(a=load, b=isRamSelected, out=shouldWriteToRam);
    RAM16K(in=in, load=shouldWriteToRam, address=address[0..13], out=ramOutput);

    Mux16(a=ramOutput, b=screenOutput, sel=isScreenSelected, out=outRamOrScreen);
    Mux16(a=outRamOrScreen, b=keyboardOutput, sel=isKeyboardSelected, out=selectedOut);
    Mux16(a=selectedOut, b=false, sel=isInvalidAddress, out=out);
}
